/*********************************************
 * OPL 12.8.0.0 Model
 * Author: sev
 * Creation Date: 6 mars 2019 at 14:51:03
 *********************************************/

using CP;

float t1;
execute {
	var before = new Date();
	t1 = before.getTime();
	cp.param.TimeLimit = 180;
}


int N=...;
tuple slot {
	int d;
	int p; 
};
range players = 1..N;
range days = 1..N-1;
range periods = 1..N div 2;
{slot} slots = {<d,p> | d in days, p in periods};

dvar int player1[slots] in players;
dvar int player2[slots] in players;

subject to {
	//  -------- player1 vs player2 = player2 vs player1
	forall(s in slots)
		player1[s] < player2[s];
	//  -------- chaque joueur joue une fois par jour
	forall(day in days, player in players)
		count(all (s in slots : s.d == day) player1[s], player) + count(all (s in slots : s.d == day) player2[s], player) == 1;
	//  -------- chaque joueur joue au plus deux fois par jour
	forall(period in periods, player in players)
		count(all (s in slots : s.p == period) player1[s], player) + count(all (s in slots : s.p == period) player2[s], player) <= 2;
}



execute  {
	var after = new Date();
	writeln("------------------------------- "+(N-1)+" jours ------------------------------- ")
	for(var p in periods) {
	write("Période "+p + " : ")	
		for(var d in days) {
			for(var s in slots) {
				if (s.p == p && s.d == d)
					write(player1[s]+", "+player2[s]+"   ");
			}
		}
		write("\n");
	}
	writeln("Temps de calcul (s) : ", (after.getTime()-t1)/1000);
}